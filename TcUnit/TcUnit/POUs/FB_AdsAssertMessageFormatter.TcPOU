<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_AdsAssertMessageFormatter" Id="{54911d7d-b4a4-43a8-a981-a2fc14a13f71}" SpecialFunc="None">
    <Declaration><![CDATA[(*
    This function block is responsible for printing the results of the assertions using the built-in
    ADSLOGSTR functionality provided by the Tc2_System library. This sends the result using ADS, which
    is consumed by the error list of Visual Studio.
*)
FUNCTION_BLOCK FB_AdsAssertMessageFormatter IMPLEMENTS I_AssertMessageFormatter]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="LogAssertFailure" Id="{48cbc6b0-5380-44b8-a045-57b6ad9f189d}">
      <Declaration><![CDATA[METHOD PUBLIC LogAssertFailure
VAR_INPUT
    Expected : Tc2_System.T_MaxString;
    Actual : Tc2_System.T_MaxString;
    Message : Tc2_System.T_MaxString;
    TestInstancePath : Tc2_System.T_MaxString;
END_VAR
VAR
    AdjustAssertFailureMessageToMax253CharLength : FB_AdjustAssertFailureMessageToMax253CharLength;
    TestInstancePathCleaned : Tc2_System.T_MaxString;
    TestInstancePathFinal : Tc2_System.T_MaxString;
    ReturnValue : DINT;
    TestInstancePathProcessed : Tc2_System.T_MaxString;
    MessageProcessed : Tc2_System.T_MaxString;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TestInstancePathCleaned := F_RemoveInstancePathAndProjectNameFromTestInstancePath(TestInstancePath);
TestInstancePathFinal := Tc2_Standard.CONCAT(STR1 := 'FAILED TEST $'', STR2 := TestInstancePathCleaned);
TestInstancePathFinal := Tc2_Standard.CONCAT(STR1 := TestInstancePathFinal, STR2 := '$'');

IF Tc2_Standard.LEN(STR := Expected) > 0 THEN
    TestInstancePathFinal := Tc2_Standard.CONCAT(STR1 := TestInstancePathFinal, STR2 := ', EXP: ');
    TestInstancePathFinal := Tc2_Standard.CONCAT(STR1 := TestInstancePathFinal, STR2 := Expected);
END_IF
IF Tc2_Standard.LEN(STR := Actual) > 0 THEN
    TestInstancePathFinal := Tc2_Standard.CONCAT(STR1 := TestInstancePathFinal, STR2 := ', ACT: ');
    TestInstancePathFinal := Tc2_Standard.CONCAT(STR1 := TestInstancePathFinal, STR2 := Actual);
END_IF
IF Tc2_Standard.LEN(STR := Message) > 0 THEN
    TestInstancePathFinal := Tc2_Standard.CONCAT(STR1 := TestInstancePathFinal, STR2 := ', MSG: %s');
END_IF

AdjustAssertFailureMessageToMax253CharLength(MsgFmtString := TestInstancePathFinal,
                                             StringArg := Message,
                                             MsgFmtStringProcessed => TestInstancePathProcessed,
                                             StringArgProcessed => MessageProcessed);

GVL_TcUnit.AdsMessageQueue.WriteLog(MsgCtrlMask := Tc2_System.ADSLOG_MSGTYPE_ERROR,
                                    MsgFmtStr := TestInstancePathProcessed,
                                    StrArg := MessageProcessed);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>