<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.9">
  <POU Name="FB_TcUnitRunner" Id="{857e16d6-a26c-468a-935e-aa7317c263b9}" SpecialFunc="None">
    <Declaration><![CDATA[(*
    This function block is responsible for holding track of the tests and executing them.
*)
FUNCTION_BLOCK FB_TcUnitRunner
VAR
    (* There can be one or more tests (testnames) for every test suite instance *)
    RunnableTestSuiteResults : ARRAY[1..GVL_Constants.MaxAmountOfTestSuites] OF E_TestSuiteRunState;

    (* Inidication of whether all test suites have reported that they are finished *)
    AllTestSuitesFinished : BOOL := FALSE;

    (* Prints the results to ADS so that Visual Studio can display the results.
       This test result formatter can be replaced with something else than ADS *)
    ADSTestResultFormatter : FB_ADSTestResultFormatter;
    TestResultPrinter : I_TestResultFormatter := ADSTestResultFormatter;

    (* These variables (statistics) are occupied once all tests are complete *)
    AmountOfTestSuitesFinished : UINT := 0;
    AmountOfTestCases : UINT := 0;
    AmountOfFailedTestCases : UINT := 0;
    AmountOfSuccessfulTestCases : UINT := 0;

    (* Indication of that the printing of test results is complete *)
    DonePrintingTestResults : BOOL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="RunTestSuiteTests" Id="{d333e6c4-a1da-4b5b-91b3-4b4808e6c299}">
      <Declaration><![CDATA[METHOD RunTestSuiteTests
VAR
    Counter : UINT := 0;
    BusyPrinting : BOOL;

    (* We need to hold a temporary state of the statistics (failed, successful, total amount of test cases etc)
       as we don't consider the tests to be completely finished until all test suites have executed completely.
       The reason we want to do it this way is because a test suite can run over several cycles. Only once all tests
       are finished (which might take many cycles), do we gather correct statistics *)
    AmountOfTestSuitesFinished_Temp : UINT := 0;
    AmountOfTestCases_Temp : UINT := 0;
    AmountOfFailedTestCases_Temp : UINT := 0;
    AmountOfSuccessfulTestCases_Temp : UINT := 0;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT AllTestSuitesFinished THEN
    IF GVL.AmountOfInitializedTestSuites = 0 THEN
        AllTestSuitesFinished := TRUE;
    ELSIF GVL.AmountOfInitializedTestSuites > 0 THEN
        FOR Counter := 1 TO GVL.AmountOfInitializedTestSuites BY 1 DO
            IF GVL.TestSuiteAddresses[Counter]^.AllTestsFinished() THEN
                AmountOfTestSuitesFinished_Temp := AmountOfTestSuitesFinished_Temp + 1;
                AmountOfTestCases_Temp := GVL.TestSuiteAddresses[Counter]^.GetAmountOfTests() + AmountOfTestCases_Temp;
                AmountOfFailedTestCases_Temp := GVL.TestSuiteAddresses[Counter]^.GetAmountOfFailedTests() + AmountOfFailedTestCases_Temp;
                AmountOfSuccessfulTestCases_Temp := GVL.TestSuiteAddresses[Counter]^.GetAmountOfSuccessfulTests() + AmountOfSuccessfulTestCases_Temp;
            ELSE
                GVL.CurrentTestSuiteBeingCalled := GVL.TestSuiteAddresses[Counter];
                GVL.CurrentTestSuiteBeingCalled^();
                RunnableTestSuiteResults[Counter] := GVL.CurrentTestSuiteBeingCalled^.GetRunState();//.GetRunnableTestSuite().RunTests();
            END_IF
        END_FOR
        IF AmountOfTestSuitesFinished_Temp = GVL.AmountOfInitializedTestSuites THEN
            (* Copy the final results over *)
            AmountOfTestSuitesFinished := AmountOfTestSuitesFinished_Temp;
            AmountOfTestCases := AmountOfTestCases_Temp;
            AmountOfFailedTestCases := AmountOfFailedTestCases_Temp;
            AmountOfSuccessfulTestCases := AmountOfSuccessfulTestCases_Temp;
            AllTestSuitesFinished := TRUE;
        END_IF
    END_IF
END_IF

IF AllTestSuitesFinished THEN
    IF NOT DonePrintingTestResults THEN
        TestResultPrinter.Format(AmountOfTestSuites := AmountOfTestSuitesFinished,
                                 AmountOfTestCases := AmountOfTestCases,
                                 AmountOfSuccessfulTestCases := AmountOfSuccessfulTestCases,
                                 AmountOfFailedTestCases := AmountOfFailedTestCases,
                                 Busy => BusyPrinting);
        DonePrintingTestResults := NOT BusyPrinting;
    END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_TcUnitRunner">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcUnitRunner.RunTestSuiteTests">
      <LineId Id="131" Count="0" />
      <LineId Id="128" Count="1" />
      <LineId Id="10" Count="1" />
      <LineId Id="69" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="83" Count="0" />
      <LineId Id="86" Count="1" />
      <LineId Id="21" Count="0" />
      <LineId Id="55" Count="0" />
      <LineId Id="183" Count="0" />
      <LineId Id="63" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="14" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="169" Count="0" />
      <LineId Id="166" Count="2" />
      <LineId Id="165" Count="0" />
      <LineId Id="24" Count="1" />
      <LineId Id="12" Count="0" />
      <LineId Id="132" Count="0" />
      <LineId Id="106" Count="1" />
      <LineId Id="143" Count="0" />
      <LineId Id="137" Count="2" />
      <LineId Id="136" Count="0" />
      <LineId Id="142" Count="0" />
      <LineId Id="148" Count="0" />
      <LineId Id="144" Count="0" />
      <LineId Id="105" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>