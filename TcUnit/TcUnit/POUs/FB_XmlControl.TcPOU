<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_XmlControl" Id="{f48b7de2-5c07-4763-a8be-3d8ab3c1f58c}" SpecialFunc="None">
    <Declaration><![CDATA[(*
Organizes parsing and composing of XML data. Data can be treated as STRING or char array.
Filebuffersize can be set via Gvl_Param_XmlControl

Usage example:

1. Initiate
    VAR
        XML : FB_XMLControl;
        Buffer: STRING(GVL_Param_XmlControl.MaxFileSize);
        //or
        Buffer: ARRAY [0..GVL_Param_XmlControl.MaxFileSize] OF BYTE;
    END_VAR

        XML.pBuffer: = ADR (buffer);
        XML.LenBuffer: = SIZEOF (buffer);

    Add your own preferred fileheader like so:
    XML: <?xml version="1.0" encoding="UTF-8"?>
   
        XML.WriteDocumentHeader( '<?xml version="1.0" encoding="UTF-8"?>');

2. Compose a tag with a parameters:
XML: <MyTag ParaName = "11" />

    XML.newTag(sTagName: = 'MyTag');
    XML.newPara(sName: = 'ParaName', sPara: = sValue);
    XML.CloseTag();


3. Add tag value:
XML: <MyTag> MyText </ MyTag>

    XML.newTag(sName := 'MyTag');
    XML.newTagData(sTagData :='MyText');
    XML.CloseTag();


4. Jump to the beginning of the XML data
XML.toStartBuffer();


5. Add a comment:
XML: <!-- MyComment -->

    XML.newComment(sTagName: = 'MyComment');

6. Returns the next tag from the current position in buffer
XML.NextTag();


7. Output the parameter of the tag
XML.NextPara(sPara: = LastValue);
Feedback: sPara returns the value found (string)

*)
FUNCTION_BLOCK FB_XmlControl
VAR
    XmlBuffer : FB_StreamBuffer;
    TagListBuffer : FB_StreamBuffer;
    Tags : T_MaxString;
    TagListSeekBuffer : FB_StreamBuffer;
    TagsSeek : STRING;
    TagBuffer : FB_StreamBuffer;
    Tag : T_MaxString;
    TagOpen: BOOL;
    Select : UDINT;
    SelectStart : UDINT;
    SelectEnd : UDINT;
    SelectStartParameter : UDINT;
    SelectEndParameter : UDINT;
    SelectTagClose : UDINT;
    SelectStartValue : UDINT;
    SelectEndValue : UDINT;
    SearchPosition : UDINT; 
END_VAR
VAR CONSTANT
    TAG_OPEN : STRING(1) := '<';
    TAG_CLOSE : STRING(1) := '>';
    END_TAG_CLOSE : STRING(4) := '/>';
    SPACE : STRING(1) := ' ';
    EQUALS : STRING(1) := '=';
    QUOTE : STRING(1) := '"';
    BACK_SLASH : STRING(1) := '\';
    FORWARD_SLASH : STRING(1) := '/';
    OPEN_COMMENT : STRING(5) := '<!-- ';
    CLOSE_COMMENT : STRING(4) := ' -->';
    TAB : STRING(2) := '$T';
	CR_LF : STRING(4) := '$R$N';
  
	//$OD : ASCII code for carriage return (CR)
	//$$ : to add a $R
	//$' : to add ' (apostrophe)
	//$L or $l : line feed
	//$N or $n : new line
	//$P or $p : next page
	//$R or $r : end of line
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="ClearBuffer" Id="{651b4460-232e-47e8-95ba-ecea1541fbb9}">
      <Declaration><![CDATA[(* 
    Clears the contents of the entire buffer
*)
METHOD PUBLIC ClearBuffer]]></Declaration>
      <Implementation>
        <ST><![CDATA[SearchPosition := 0;
TagListSeekBuffer.Length := 0;
XmlBuffer.Length := 0;
TagsSeek := '';
Tag := '';]]></ST>
      </Implementation>
    </Method>
    <Method Name="CloseTag" Id="{eedd2aac-2425-4325-8128-bfe1e984f3f7}">
      <Declaration><![CDATA[(*
    Closes a Tag:
    XML: <MyTag />'

    Method: XML.CloseTag();
*)
METHOD PUBLIC CloseTag : T_MaxString]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF TagOpen THEN
    XmlBuffer.Append := END_TAG_CLOSE;
    Select := TagListBuffer.FindBack(SearchString := FORWARD_SLASH);
    CloseTag := TagListBuffer.CutOff(udiStartPos:= Select);
    TagOpen := FALSE;
ELSE
    Select := TagListBuffer.FindBack(SearchString := FORWARD_SLASH);
    CloseTag := TagListBuffer.CutOff(udiStartPos:= Select);
    XmlBuffer.Append := TAG_OPEN;
    XmlBuffer.Append := CloseTag;
    XmlBuffer.Append := TAG_CLOSE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="Length" Id="{4fdf30b6-5fe7-4452-ad0a-85e866e236d0}">
      <Declaration><![CDATA[PROPERTY Length : UDINT]]></Declaration>
      <Get Name="Get" Id="{267105d4-b124-4c5c-a359-0434c4992afc}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Length := XmlBuffer.Length;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="NewComment" Id="{0f41fa45-b92e-4e7e-b2b3-ecb5d5918326}">
      <Declaration><![CDATA[(*
    Adds a comment;
    XML: <!-- MyComment -->

    XML.NewComment(Comment: = 'MyComment');
*)
METHOD PUBLIC NewComment
VAR_INPUT
    Comment : T_MaxString;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF TagOpen THEN
    XmlBuffer.Append := TAG_CLOSE;
    TagOpen := FALSE;
END_IF;
XmlBuffer.Append := OPEN_COMMENT;
XmlBuffer.Append := Comment;
XmlBuffer.Append := CLOSE_COMMENT;]]></ST>
      </Implementation>
    </Method>
    <Method Name="NewParameter" Id="{60c10606-ff09-4f9b-a3dd-0b54d0f565c7}">
      <Declaration><![CDATA[(*
    Must be called after opening a new tag

    XML.NewParameter(Name: = 'ParaName', Value: = 'Value');
*)
METHOD PUBLIC NewParameter
VAR_INPUT
    Name : T_MaxString;
    Value : T_MaxString;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[XmlBuffer.Append := SPACE;  
XmlBuffer.Append := Name;
XmlBuffer.Append := EQUALS; 
XmlBuffer.Append := QUOTE; 
XmlBuffer.Append := Value;
XmlBuffer.Append := QUOTE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="NewTag" Id="{c44bdbda-725a-4943-a5cc-fa5ea9485541}">
      <Declaration><![CDATA[(*
    Creates a new Tag:
    XML: <MyTag>

    XML.NewTag(Name: = 'MyTag');
*)
METHOD PUBLIC NewTag
VAR_INPUT
    Name : T_MaxString;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF TagOpen THEN
    XmlBuffer.Append := TAG_CLOSE;
END_IF;
XmlBuffer.Append := TAG_OPEN;
XmlBuffer.Append := Name;
TagOpen := TRUE;
TagListBuffer.Append := FORWARD_SLASH;
TagListBuffer.Append := Name;]]></ST>
      </Implementation>
    </Method>
    <Method Name="NewTagData" Id="{e4679d3f-10eb-47fd-936c-97e3aaaa3768}">
      <Declaration><![CDATA[METHOD PUBLIC NewTagData
VAR_INPUT
    Data : T_MaxString;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[XmlBuffer.Append := TAG_CLOSE;
XmlBuffer.Append := Data;
TagOpen := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="NextParameter" Id="{4394d0a2-f7bc-4847-9625-00022c462a79}">
      <Declaration><![CDATA[METHOD PUBLIC NextParameter : T_MaxString;
VAR_OUTPUT
    Parameter : T_MaxString;
END_VAR
VAR
    Name : T_MaxString;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[SelectEndParameter := XmlBuffer.Find(SearchString := EQUALS, StartPos := SelectStartParameter);
IF SelectStartParameter < SelectEnd AND SelectEndParameter < SelectEnd THEN
    Name := XmlBuffer.Copy( StartPos := SelectStartParameter + 1, EndPos := SelectEndParameter);
    SelectStartValue := XmlBuffer.Find(SearchString := QUOTE, StartPos := SelectEndParameter);
    SelectEndValue := XmlBuffer.Find(SearchString := QUOTE, StartPos := SelectStartValue);
    Parameter := XmlBuffer.Copy( StartPos := SelectStartValue + 1, EndPos := SelectEndValue);
    SelectStartParameter := SelectEndValue + 1;
ELSE
    SelectEndValue := XmlBuffer.Find(SearchString := TAG_OPEN, StartPos := SearchPosition);
    Name := XmlBuffer.Copy( StartPos := SearchPosition + 1, EndPos := SelectEndValue); 
END_IF;
NextParameter := Name;]]></ST>
      </Implementation>
    </Method>
    <Method Name="NextTag" Id="{d039b781-905d-4b69-b5aa-bb0323e598de}">
      <Declaration><![CDATA[METHOD PUBLIC NextTag : T_MaxString]]></Declaration>
      <Implementation>
        <ST><![CDATA[SelectStart := XmlBuffer.Find(SearchString := TAG_OPEN, StartPos := SearchPosition);
SelectEnd := XmlBuffer.Find(SearchString := TAG_CLOSE, StartPos := SelectStart);
SelectStartParameter := XmlBuffer.Find(SearchString := SPACE, StartPos := SelectStart);
SelectTagClose := XmlBuffer.Find(SearchString := FORWARD_SLASH, StartPos := SelectStart);
IF SelectStart < XmlBuffer.Length AND SelectEnd < XmlBuffer.Length THEN
    SearchPosition := SelectEnd;
    Tag := XmlBuffer.Copy( StartPos:= SelectStart + 1, EndPos:= SelectEnd);
    IF SelectEnd < SelectStartParameter THEN
        NextTag := XmlBuffer.Copy( StartPos:= SelectStart + 1, EndPos:= SelectEnd);
    ELSE
        NextTag := XmlBuffer.Copy( StartPos:= SelectStart + 1, EndPos:= SelectStartParameter);
    END_IF;
    IF SelectTagClose = SelectStart + 1 THEN
        Select := TagListSeekBuffer.FindBack(SearchString := FORWARD_SLASH);
        TagListSeekBuffer.CutOff(udiStartPos:= Select);
    ELSIF SelectTagClose > SelectStart AND SelectTagClose < SelectEnd THEN
        TagListSeekBuffer.Append := FORWARD_SLASH;
        TagListSeekBuffer.Append := NextTag;
        Select := TagListSeekBuffer.FindBack(SearchString := FORWARD_SLASH);
        TagListSeekBuffer.CutOff(udiStartPos:= Select);
    ELSE
        TagListSeekBuffer.Append := FORWARD_SLASH;
        TagListSeekBuffer.Append := NextTag;
    END_IF;
ELSE
    SearchPosition := XmlBuffer.Length;
    NextTag := '';
END_IF;]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetBuffer" Id="{cbfaa6d5-eab9-48d0-a339-a8bf334e344e}">
      <Declaration><![CDATA[METHOD PUBLIC SetBuffer
VAR_INPUT
    PointerToBuffer : POINTER TO BYTE; // ADR(..)
    SizeOfBuffer : UDINT; // SIZEOF(..)
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[XmlBuffer.SetBuffer(PointerToStringBuf := PointerToBuffer, BufSize := SizeOfBuffer);
TagListBuffer.SetBuffer(PointerToStringBuf := ADR(Tags), BufSize := SIZEOF(Tags));
TagListSeekBuffer.SetBuffer(PointerToStringBuf := ADR(TagsSeek), BufSize := SIZEOF(TagsSeek));
TagBuffer.SetBuffer(PointerToStringBuf := ADR(Tag), BufSize := SIZEOF(Tag));]]></ST>
      </Implementation>
    </Method>
    <Method Name="ToStartBuffer" Id="{cb065068-a6c8-48d5-8c1d-535a21365348}">
      <Declaration><![CDATA[(*
    Jump to the beginning of the XML data
    XML.ToStartBuffer();
*)
METHOD PUBLIC ToStartBuffer]]></Declaration>
      <Implementation>
        <ST><![CDATA[SearchPosition := 0;
TagListSeekBuffer.Length := 0;
TagsSeek := '';
Tag := '';]]></ST>
      </Implementation>
    </Method>
    <Method Name="WriteDocumentHeader" Id="{14bf2549-9e7d-451f-8ab5-fe662eb30712}">
      <Declaration><![CDATA[(*
    Add your own preffered fileheader like:
    XML: <?xml version="1.0" encoding="UTF-8"?>
    
    Start with calling this method before appending any other tags!

    XML.WriteDocumentHeader('<?xml version="1.0" encoding="UTF-8"?>');
*)
METHOD PUBLIC WriteDocumentHeader
VAR_INPUT
    Header : T_MaxString;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[XmlBuffer.Append := Header;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>