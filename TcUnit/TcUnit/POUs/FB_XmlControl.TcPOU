<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_XmlControl" Id="{f48b7de2-5c07-4763-a8be-3d8ab3c1f58c}" SpecialFunc="None">
    <Declaration><![CDATA[(*
    Organizes parsing and composing of XML data. Data can be treated as STRING or char array.
    Buffer size of file can be set via GVL_Param_TcUnit (xUnitBufferSize)
*)
FUNCTION_BLOCK FB_XmlControl
VAR
    XmlBuffer : FB_StreamBuffer;
    TagListBuffer : FB_StreamBuffer;
    Tags : Tc2_System.T_MaxString;
    TagListSeekBuffer : FB_StreamBuffer;
    TagsSeek : STRING;
    TagBuffer : FB_StreamBuffer;
    Tag : Tc2_System.T_MaxString;
    TagOpen : BOOL;
    Select : UDINT;
    SearchPosition : UDINT;
END_VAR
VAR CONSTANT
    TAG_OPEN : STRING(1) := '<';
    TAG_CLOSE : STRING(1) := '>';
    END_TAG_CLOSE : STRING(2) := '/>';
    SPACE : STRING(1) := ' ';
    EQUALS : STRING(1) := '=';
    QUOTE : STRING(1) := '"';
    BACK_SLASH : STRING(1) := '\';
    FORWARD_SLASH : STRING(1) := '/';
    OPEN_COMMENT : STRING(5) := '<!-- ';
    CLOSE_COMMENT : STRING(4) := ' -->';
    TAB : STRING(2) := '$T';
    CR_LF : STRING(4) := '$R$N';
    
    // $OD : ASCII code for carriage return (CR)
    // $$ : to add a $R
    // $' : to add ' (apostrophe)
    // $L or $l : line feed
    // $N or $n : new line
    // $P or $p : next page
    // $R or $r : end of line
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="ClearBuffer" Id="{651b4460-232e-47e8-95ba-ecea1541fbb9}">
      <Declaration><![CDATA[(* 
    Clears the contents of the entire buffer.
*)
METHOD PUBLIC ClearBuffer
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SearchPosition := 0;
TagListSeekBuffer.Length := 0;
XmlBuffer.Length := 0;
TagsSeek := '';
Tag := '';]]></ST>
      </Implementation>
    </Method>
    <Method Name="CloseTag" Id="{eedd2aac-2425-4325-8128-bfe1e984f3f7}">
      <Declaration><![CDATA[(*
    Closes a Tag:
    XML: <MyTag />'

Method : XML.CloseTag();
*)
METHOD PUBLIC CloseTag : Tc2_System.T_MaxString
VAR
    ClosedTag : Tc2_System.T_MaxString;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF TagOpen THEN
    XmlBuffer.Append := END_TAG_CLOSE;
    Select := TagListBuffer.FindBack(SearchString := FORWARD_SLASH);
    ClosedTag := TagListBuffer.CutOff(StartPos := Select);
    TagOpen := FALSE;
ELSE
    Select := TagListBuffer.FindBack(SearchString := FORWARD_SLASH);
    ClosedTag := TagListBuffer.CutOff(StartPos := Select);
    XmlBuffer.Append := TAG_OPEN;
    XmlBuffer.Append := ClosedTag;
    XmlBuffer.Append := TAG_CLOSE;
END_IF

CloseTag := ClosedTag;]]></ST>
      </Implementation>
    </Method>
    <Property Name="Length" Id="{4fdf30b6-5fe7-4452-ad0a-85e866e236d0}">
      <Declaration><![CDATA[PROPERTY Length : UDINT
]]></Declaration>
      <Get Name="Get" Id="{267105d4-b124-4c5c-a359-0434c4992afc}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[Length := XmlBuffer.Length;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="NewComment" Id="{0f41fa45-b92e-4e7e-b2b3-ecb5d5918326}">
      <Declaration><![CDATA[(*
    Adds a comment
    XML: <!-- MyComment -->

    XML.NewComment(Comment: = 'MyComment');
*)
METHOD PUBLIC NewComment
VAR_INPUT
    Comment : Tc2_System.T_MaxString;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF TagOpen THEN
    XmlBuffer.Append := TAG_CLOSE;
    TagOpen := FALSE;
END_IF;
XmlBuffer.Append := OPEN_COMMENT;
XmlBuffer.Append := Comment;
XmlBuffer.Append := CLOSE_COMMENT;]]></ST>
      </Implementation>
    </Method>
    <Method Name="NewParameter" Id="{60c10606-ff09-4f9b-a3dd-0b54d0f565c7}">
      <Declaration><![CDATA[(*
    Must be called after opening a new tag

    XML.NewParameter(Name: = 'ParaName', Value: = 'Value');
*)
METHOD PUBLIC NewParameter
VAR_INPUT
    Name : Tc2_System.T_MaxString;
    Value : Tc2_System.T_MaxString;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[XmlBuffer.Append := SPACE;
XmlBuffer.Append := Name;
XmlBuffer.Append := EQUALS;
XmlBuffer.Append := QUOTE;
XmlBuffer.Append := Value;
XmlBuffer.Append := QUOTE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="NewTag" Id="{c44bdbda-725a-4943-a5cc-fa5ea9485541}">
      <Declaration><![CDATA[(*
    Creates a new Tag:
    XML: <MyTag>

    XML.NewTag(Name: = 'MyTag');
*)
METHOD PUBLIC NewTag
VAR_INPUT
    Name : Tc2_System.T_MaxString;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF TagOpen THEN
    XmlBuffer.Append := TAG_CLOSE;
END_IF;
XmlBuffer.Append := TAG_OPEN;
XmlBuffer.Append := Name;
TagOpen := TRUE;
TagListBuffer.Append := FORWARD_SLASH;
TagListBuffer.Append := Name;]]></ST>
      </Implementation>
    </Method>
    <Method Name="NewTagData" Id="{e4679d3f-10eb-47fd-936c-97e3aaaa3768}">
      <Declaration><![CDATA[METHOD PUBLIC NewTagData
VAR_INPUT
    Data : Tc2_System.T_MaxString;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[XmlBuffer.Append := TAG_CLOSE;
XmlBuffer.Append := Data;
TagOpen := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetBuffer" Id="{cbfaa6d5-eab9-48d0-a339-a8bf334e344e}">
      <Declaration><![CDATA[METHOD PUBLIC SetBuffer
VAR_INPUT
    PointerToBuffer : POINTER TO BYTE; // ADR(..)
    SizeOfBuffer : UDINT; // SIZEOF(..)
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[XmlBuffer.SetBuffer(PointerToBufferAddress:= PointerToBuffer, SizeOfBuffer := SizeOfBuffer);
TagListBuffer.SetBuffer(PointerToBufferAddress := ADR(Tags), SizeOfBuffer := SIZEOF(Tags));
TagListSeekBuffer.SetBuffer(PointerToBufferAddress := ADR(TagsSeek), SizeOfBuffer := SIZEOF(TagsSeek));
TagBuffer.SetBuffer(PointerToBufferAddress := ADR(Tag), SizeOfBuffer := SIZEOF(Tag));]]></ST>
      </Implementation>
    </Method>
    <Method Name="ToStartBuffer" Id="{cb065068-a6c8-48d5-8c1d-535a21365348}">
      <Declaration><![CDATA[(*
    Jump to the beginning of the XML data
    XML.ToStartBuffer();
*)
METHOD PUBLIC ToStartBuffer
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SearchPosition := 0;
TagListSeekBuffer.Length := 0;
TagsSeek := '';
Tag := '';]]></ST>
      </Implementation>
    </Method>
    <Method Name="WriteDocumentHeader" Id="{14bf2549-9e7d-451f-8ab5-fe662eb30712}">
      <Declaration><![CDATA[(*
    Add your own preffered fileheader like:
    XML: <?xml version="1.0" encoding="UTF-8"?>

    Start with calling this method before appending any other tags!

    XML.WriteDocumentHeader('<?xml version="1.0" encoding="UTF-8"?>');
*)
METHOD PUBLIC WriteDocumentHeader
VAR_INPUT
    Header : Tc2_System.T_MaxString;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[XmlBuffer.Append := Header;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>